plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.9.23'
    id 'edu.sc.seis.launch4j' version '3.0.5'
}

group 'njoh.botgang'
version '1.0'

repositories {
    mavenCentral()
    maven {
        url 'https://repo.repsy.io/mvn/njoh/public'
    }
}

dependencies {
    implementation 'no.njoh:pulse-engine:0.9.0'
    implementation(files("libs/bot-coding-challenge.jar"))
}

kotlin {
    jvmToolchain(19)
}

////////////////////////////////////////////////////////////////////////////////// Windows release

String releaseName     = "$name-$version"
String releaseBuildDir = "$buildDir/$releaseName"
String mainClass       = 'MainKt'

jar {
    duplicatesStrategy DuplicatesStrategy.INCLUDE
    from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    exclude 'macos/**', 'linux/**' // Exclude natives for Mac and Linux when creating Windows exe
    manifest { attributes('Main-Class': mainClass) }
}

launch4j {
    mainClassName  = mainClass
    bundledJrePath = 'jre'
    icon           = "${projectDir}/icon.ico"
    outputDir      = releaseBuildDir
}

task buildWin64Release(type: Zip) {
    group 'release'
    dependsOn 'createExe'
    doFirst {
        delete "$releaseBuildDir/lib"
        copy {
            from zipTree('jre/jre19-win64.zip')
            into "$releaseBuildDir/jre"
        }
    }
    from releaseBuildDir
    destinationDir file('release/win64')
    archiveName "${releaseName}.zip"
}